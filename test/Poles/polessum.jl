using Distributions: Normal, pdf
using RAS_DMFT
using LinearAlgebra
using Test

@testset "PolesSum" begin
    @testset "constructor" begin
        @testset "inner constructor" begin
            locs = [0, 1]
            wgts = [2, 3]
            P = PolesSum{Int, Int}(locs, wgts)
            @test P isa PolesSum{Int, Int}
            @test P.locations === locs
            @test P.weights === wgts
            # length mismatch
            @test_throws DimensionMismatch PolesSum{Int, Int}(rand(3), rand(4))
            @test_throws DimensionMismatch PolesSum{Int, Int}(rand(4), rand(3))
            PolesSumBlock{Int, Float64}(Int[], Float64[]) # empty lists
        end # inner constructor

        @testset "outer constructors" begin
            # canonical form
            locs = [0, 1]
            wgts = [2, 3]
            P = PolesSum(locs, wgts)
            @test P.locations == locs
            @test P.locations !== locs
            @test P.weights == wgts
            @test P.weights !== wgts

            # sort and merge degenerate poles
            locs = [0, 1, 0]
            wgts = [2, 3, 4]
            P = PolesSum(locs, wgts)
            @test P.locations == [0, 1]
            @test P.weights == [6, 3]

            # conversion of type
            locs = [0, 1]
            wgts = [2, 3]
            P = PolesSum(locs, wgts)
            P_new = PolesSum{UInt, Float64}(P)
            @test P_new isa PolesSum{UInt, Float64}
            @test P_new.locations == locs
            @test P_new.weights == wgts
        end # outer constructors
    end # constructor

    @testset "custom functions" begin
        @testset "add_pole_at_zero!" begin
            # pole already exists
            P = PolesSum([-1, 0, 2], [3, 4, 5])
            @test add_pole_at_zero!(P) === P
            @test locations(P) == [-1, 0, 2]
            @test weights(P) == [3, 4, 5]
            # pole does not exist
            P = PolesSum([-1, 2], [3, 5])
            @test add_pole_at_zero!(P) === P
            @test locations(P) == [-1, 0, 2]
            @test weights(P) == [3, 0, 5]
        end # add_pole_at_zero!

        @testset "amplitude" begin
            locs = 0:5
            wgts = 5:10
            P = PolesSum(locs, wgts)
            @test_throws BoundsError amplitude(P, 0)
            @test amplitude(P, 1) == sqrt(5)
            @test amplitude(P, 2) == sqrt(6)
            @test amplitude(P, 3) == sqrt(7)
            @test amplitude(P, 4) == sqrt(8)
            @test amplitude(P, 5) == sqrt(9)
            @test amplitude(P, 6) == sqrt(10)
            @test_throws BoundsError amplitude(P, 7)

            # complex weight
            locs = [1]
            wgts = [5 + 3im]
            P = PolesSum(locs, wgts)
            @test_throws DomainError amplitude(P, 1)
        end # amplitude

        @testset "amplitudes" begin
            locs = 0:5
            wgts = 5:10
            P = PolesSum(locs, wgts)
            @test amplitudes(P) == sqrt.(5:10)
            # errors
            P = PolesSum(locs, -wgts)
            @test_throws DomainError amplitudes(P)
            P = PolesSum(locs, rand(ComplexF64, 6))
            @test_throws DomainError amplitudes(P)
        end # amplitudes

        @testset "evaluate" begin
            locs = [-1.0, 0.0, 2.0]
            wgts = [0.2, 0.3, 0.5]
            P = PolesSum(locs, wgts)
            # upper/lower complex plane
            z = 0.5 + 1.0im
            @test evaluate(P, z) ≈ -0.018461538461538474 - 0.4553846153846154im atol =
                10 * eps()
            @test evaluate(P, conj(z)) == conj(evaluate(P, z))
            # Matsubara frequency
            z = 2im
            @test evaluate(P, z) ≈ -0.085 - 0.355im atol =
                10 * eps()
            @test evaluate(P, conj(z)) == conj(evaluate(P, z))
            # grid
            zs = [0.1 + 0.5im, 0.3 + 0.5im]
            @test evaluate(P, zs) == [evaluate(P, zs[1]), evaluate(P, zs[2])]
        end # evaluate

        @testset "evaluate_gaussian" begin
            locs = [-1.0, 0.0, 2.0]
            wgts = [0.2, 0.3, 0.5]
            P = PolesSum(locs, wgts)
            # single point
            ω = 0.5
            σ = 1.0
            @test evaluate_gaussian(P, ω, σ) ≈
                -0.08753757822014871 - 0.6166378221821291im atol = 10 * eps()
            # grid
            ω = [0.1, 0.3]
            @test evaluate_gaussian(P, ω, 0.5) ==
                [evaluate_gaussian(P, ω[1], 0.5), evaluate_gaussian(P, ω[2], 0.5)]
        end # evaluate_gaussian

        @testset "evaluate_lorentzian" begin
            locs = [-1.0, 0.0, 2.0]
            wgts = [0.2, 0.3, 0.5]
            P = PolesSum(locs, wgts)
            # single point
            @test evaluate_lorentzian(P, 0.5, 1) ≈
                -0.018461538461538474 - 0.4553846153846154im atol = 10 * eps()
            # grid
            ω = [0.1, 0.3]
            @test evaluate_lorentzian(P, ω, 0.5) ==
                [evaluate_lorentzian(P, ω[1], 0.5), evaluate_lorentzian(P, ω[2], 0.5)]
        end # evaluate_lorentzian

        @testset "filling" begin
            locs = -1:1
            wgts = 4:6
            P = PolesSum(locs, wgts)
            @test filling(P) === 6.5
            @test filling(P, -Inf) === 0.0
            @test filling(P, -1.1) === 0.0
            @test filling(P, -1) === 2.0
            @test filling(P, 0) === 6.5
            @test filling(P, 1) === 12.0
            @test filling(P, 1.1) === 15.0
            @test filling(P, Inf) === 15.0
        end # filling

        @testset "flip_spectrum!" begin
            P = PolesSum([0.1, 0.2], [0.3, 0.4])
            @test flip_spectrum!(P) === P
            @test locations(P) == [-0.2, -0.1]
            @test weights(P) == [0.4, 0.3]
        end # flip_spectrum!

        @testset "flip_spectrum" begin
            P = PolesSum([0.1, 0.2], [0.3, 0.4])
            foo = flip_spectrum(P)
            @test foo !== P
            @test locations(foo) == [-0.2, -0.1]
            @test weights(foo) == [0.4, 0.3]
        end # flip_spectrum

        @testset "inverse" begin
            grid = range(-1, 1; length = 101)
            G = greens_function_bethe_grid(grid)
            a0, P = inverse(G)
            @test length(P) === 100 # originally 101 poles
            # poles are symmetric
            @test abs(a0) < eps()
            @test norm(locations(P) + reverse(locations(P))) < 50 * eps()
            @test norm(weights(P) - reverse(weights(P))) < 10 * eps()
            @test moment(P, 0) ≈ 0.25 atol = 1.0e-4 # total weight
            # evaluate
            δ = 0.1
            @test norm(
                evaluate_lorentzian(G, 0, δ) -
                    1 / (im * δ - a0 - evaluate_lorentzian(P, 0, δ)),
            ) < 50 * eps()
            ω = 1.0
            δ = 0.1
            @test norm(
                evaluate_lorentzian(G, ω, δ) -
                    1 / (ω + im * δ - a0 - evaluate_lorentzian(P, ω, δ)),
            ) < 10 * eps()
            # symmetry
            z1 = 1 / (-0.8 + 0.1im - a0 - evaluate_lorentzian(P, -0.8, 0.1))
            z2 = 1 / (0.8 + 0.1im - a0 - evaluate_lorentzian(P, 0.8, 0.1))
            @test real(z1) ≈ -real(z2) rtol = 20 * eps()
            @test imag(z1) ≈ imag(z2) rtol = 20 * eps()
        end # inverse

        @testset "location" begin
            P = PolesSum(0:5, 5:10)
            @test location(P, 1) == 0
            @test location(P, 2) == 1
            @test location(P, 3) == 2
            @test location(P, 4) == 3
            @test location(P, 5) == 4
            @test location(P, 6) == 5
            @test_throws BoundsError location(P, 7)
        end # location

        @testset "locations" begin
            P = PolesSum(0:5, 5:10)
            @test locations(P) === P.locations
        end # locations

        @testset "merge_degenerate_poles!" begin
            P = PolesSum([0.2, 0.3, 0.6], [0.0625, 0.5625, 2.25])
            # manual tolerance
            P1 = copy(P)
            @test merge_degenerate_poles!(P1, 0.11) === P1
            @test locations(P1) == [0.2, 0.6]
            @test weights(P1) == [0.625, 2.25]
            # default tolerance too small
            P1 = copy(P)
            merge_degenerate_poles!(P1)
            @test locations(P1) == [0.2, 0.3, 0.6]
            @test weights(P1) == [0.0625, 0.5625, 2.25]
            # custom tolerance
            P1 = copy(P)
            locations(P1)[2] = 0.5999999999999
            merge_degenerate_poles!(P1, 1.0e-10)
            @test locations(P1) == [0.2, 0.5999999999999]
            @test weights(P1) == [0.0625, 2.8125]
            # negative locations
            P = PolesSum([-0.6, -0.3, -0.2], [2.25, 0.5625, 0.0625])
            merge_degenerate_poles!(P, 0.11)
            @test locations(P) == [-0.6, -0.2]
            @test weights(P) == [2.25, 0.625]
            # poles around zero
            P = PolesSum([-0.03, -0.01, 0.01, 0.05], [0.0625, 0.5625, 2.25, 6.25])
            merge_degenerate_poles!(P, 0.04)
            @test locations(P) == [0.0, 0.05]
            @test weights(P) == [2.875, 6.25]
            # poles at exactly same location
            P = PolesSum([-0.5, -0.5, 0.0, 0.05], [0.0625, 0.5625, 2.25, 6.25])
            merge_degenerate_poles!(P)
            @test locations(P) == [-0.5, 0.0, 0.05]
            @test weights(P) == [0.625, 2.25, 6.25]
        end # merge_degenerate_poles!

        @testset "merge_negative_locations_to_zero!" begin
            P = PolesSum([-0.1, -0.0, 0.0, 0.2], [0.25, 0.5625, 6.25, 1.5])
            @test merge_negative_locations_to_zero!(P) === P
            @test locations(P) == [0.0, 0.2]
            @test weights(P) == [7.0625, 1.5]
            # degeneracy at zero
            P = PolesSum([-0.0, -0.0, 0.0, 0.2], [0.25, 0.5625, 6.25, 1.5])
            @test merge_negative_locations_to_zero!(P) === P
            @test locations(P) == [0.0, 0.2]
            @test weights(P) == [7.0625, 1.5]
            # # no negative location
            P = PolesSum([0.1, 0.5, 1.0], [0.5, 2.5, 1.5])
            @test merge_negative_locations_to_zero!(P) === P
            @test locations(P) == [0.1, 0.5, 1.0]
            @test weights(P) == [0.5, 2.5, 1.5]
        end # merge_negative_locations_to_zero!

        @testset "merge_negative_weight!" begin
            # equidistant grid
            locs = [-0.5, 0.5, 1.5]
            wgts = [1.5, -0.5, 5.0]
            P = PolesSum(locs, wgts)
            @test merge_negative_weight!(P) === P
            @test locations(P) == [-0.5, 1.5]
            @test weights(P) == [1.25, 4.75]

            # not equidistant grid
            locs = [-0.5, 0.0, 1.5]
            wgts = [1.5, -0.5, 5.0]
            P = merge_negative_weight!(PolesSum(locs, wgts))
            @test locations(P) == [-0.5, 1.5]
            @test weights(P) == [1.125, 4.875]

            # first pole negative
            locs = [0.0, 1.0, 5.0]
            wgts = [-1.0, 0.5, 2.25]
            P = merge_negative_weight!(PolesSum(locs, wgts))
            @test locations(P) == [5.0]
            @test weights(P) == [1.75]

            # last pole negative
            locs = [0.0, 1.0, 5.0]
            wgts = [2.25, 0.5, -1.0]
            P = merge_negative_weight!(PolesSum(locs, wgts))
            @test locations(P) == [0.0]
            @test weights(P) == [1.75]

            # total weight zero
            locs = [0.0, 1.0, 5.0]
            wgts = [-1.0, 0.5, 0.5]
            P = merge_negative_weight!(PolesSum(locs, wgts))
            @test locations(P) == Float64[]
            @test weights(P) == Float64[]

            # symmetric case
            locs = [-2.0, -0.5, 0.0, 0.5, 2.0]
            wgts = [4.0, -2.0, 1.0, -2.0, 4.0]
            P = merge_negative_weight!(PolesSum(locs, wgts))
            @test locations(P) == [-2.0, 2.0]
            @test weights(P) == [2.5, 2.5]

            # previous pole would get negative weight
            locs = [-1.0, -0.5, 0.0, 1.5]
            wgts = [2.0, 1.5, -2.5, 5.0]
            P = merge_negative_weight!(PolesSum(locs, wgts))
            @test locations(P) == [-1.0, 1.5]
            @test weights(P) == [1.7, 4.3]
        end # merge_negative_weight!

        @testset "merge_small_weight!" begin
            P = PolesSum([-1.0, 0.0, 1.5], [0.25, 6.25, 5.0])
            # tolerance small
            @test merge_small_weight!(P, eps()) === P
            @test locations(P) == [-1.0, 0.0, 1.5]
            @test weights(P) == [0.25, 6.25, 5.0]
            # first index
            merge_small_weight!(P, 1.0)
            @test locations(P) == [0.0, 1.5]
            @test weights(P) == [6.5, 5.0]
            # last index
            P = PolesSum([-1.0, 0.0, 1.5], [5.0, 6.25, 0.25])
            merge_small_weight!(P, 1.0)
            @test locations(P) == [-1.0, 0.0]
            @test weights(P) == [5.0, 6.5]
            # middle index
            P = PolesSum([-1.0, 0.0, 1.5], [25.0, 0.25, 6.25])
            merge_small_weight!(P, 1.0)
            @test locations(P) == [-1.0, 1.5]
            @test weights(P) == [25.15, 6.35]
            # merge zero weight
            P = PolesSum([0, 2], [0, 1])
            merge_small_weight!(P, 0)
            @test locations(P) == [2]
            @test weights(P) == [1]
        end # merge_small_weight!

        @testset "moment" begin
            P = PolesSum([-0.5, 0.0, 0.5], [0.0625, 2.25, 0.0625])
            @test moment(P) == 2.375
            @test iszero(moment(P, 1))
            @test moment(P, 2) == 0.03125
            @test iszero(moment(P, 101))
            # odd moment must vanish for even function
            P = PolesSum([-1.0, -eps(), -2.0, 2.0, eps(), 1.0], fill(1.0, 6))
            @test iszero(moment(P, 1))
            @test iszero(moment(P, 101))
        end # moment

        @testset "moments" begin
            P = PolesSum([-0.5, 0.0, 0.5], [0.0625, 2.25, 0.0625])
            @test moments(P, 0:1) == [2.375, 0]
            @test all(iszero, moments(P, 1:2:11))
        end # moments

        @testset "remove_zero_weight!" begin
            P = PolesSum(1:6, [0, 7, 0, 9, 0, -0.0])
            @test remove_zero_weight!(P) === P
            @test locations(P) == [2, 4]
            @test weights(P) == [7, 9]
            # pole at origin
            locs = [-1, 0, 1]
            wgts = [2, 0, 0]
            P = PolesSum(copy(locs), copy(wgts))
            remove_zero_weight!(P)
            @test locations(P) == [-1]
            @test weights(P) == [2]
            P = PolesSum(copy(locs), copy(wgts))
            remove_zero_weight!(P, false)
            @test locations(P) == [-1, 0]
            @test weights(P) == [2, 0]
        end # remove_zero_weight!

        @testset "remove_zero_weight" begin
            P = PolesSum(1:6, [0, 7, 0, 9, 0, -0.0])
            P_new = remove_zero_weight(P)
            @test P_new !== P
            @test locations(P_new) == [2, 4]
            @test weights(P_new) == [7, 9]
            @test locations(P) == 1:6
            @test weights(P) == [0, 7, 0, 9, 0, 0]
        end # remove_zero_weight

        @testset "shift_spectrum!" begin
            P = PolesSum(1:6, [0, 7, 0, 9, 0, -0.0])
            @test RAS_DMFT.shift_spectrum!(P, 2) === P
            @test locations(P) == -1:4
            @test weights(P) == [0, 7, 0, 9, 0, -0.0]
        end # shift_spectrum!

        @testset "spectral_function_loggauss" begin
            Λ = 1.2
            N = 150
            locs = grid_log(1, Λ, N)
            grid = [-reverse(locs); 0; locs]
            G = greens_function_bethe_grid(grid)
            W = range(-2; stop = 2, length = 4000) # exclude ω == 0
            P = spectral_function_loggaussian(G, W, 0.2)
            P .*= π
            @test all(>=(0), P) # positive semidefinite
            @test norm(P - reverse(P)) / 4000 < 10 * eps() # symmetry
            @test abs(P[2000] - 2) < 0.01 # Luttinger pinning
            @test first(P) < 1.0e-6 # decay for ω → ±∞
            @test last(P) < 1.0e-6 # decay for ω → ±∞

            # a zero pole is broadened with a normalized Gaussian
            # whose width is the smallest nonzero pole location, here σ = 1
            G0 = PolesSum([-1.0, 0.0, 1.0], [0.25, 0.5, 0.25])
            G1 = PolesSum([-1.0, 1.0], [0.25, 0.25])
            gauss = 0.5 .* pdf.(Normal(0.0, 1.0), W)
            @test spectral_function_loggaussian(G0, W, 0.2) ==
                spectral_function_loggaussian(G1, W, 0.2) .+ gauss
            # at ω = 0 only the Gaussian contributes
            @test spectral_function_loggaussian(G0, 0.0, 0.2) ≈ 0.5 / sqrt(2π) atol =
                10 * eps()
            # a single nonzero pole suffices to set the width
            G_edge = PolesSum([0.0, 1.0], [0.3, 0.7])
            @test spectral_function_loggaussian(G_edge, 0.4, 0.2) ==
                spectral_function_loggaussian(PolesSum([1.0], [0.7]), 0.4, 0.2) +
                0.3 * pdf(Normal(0.0, 1.0), 0.4)
            # a lone zero pole cannot be broadened
            @test_throws ArgumentError spectral_function_loggaussian(
                PolesSum([0.0], [1.0]), 0.5, 0.2
            )
        end # spectral_function_loggauss

        @testset "to_grid" begin
            # all poles within grid, middle pole centered
            P = PolesSum([0.1, 0.2, 0.3], [25.0, 100.0, 1.0])
            grid = [0.1, 0.3]
            foo = to_grid(P, grid)
            @test locations(foo) !== grid
            @test locations(foo) == [0.1, 0.3]
            @test norm(weights(foo) - [75, 51]) < 100 * eps()
            # all poles within grid, middle pole not centered
            P = PolesSum([0.1, 0.25, 0.3], [25.0, 100.0, 1.0])
            grid = [0.1, 0.3]
            foo = to_grid(P, grid)
            @test locations(foo) == [0.1, 0.3]
            @test weights(foo) == [50, 76]
            # pole outside grid
            P = PolesSum([0.0, 1.0], [25.0, 100.0])
            grid = [0.1, 0.3]
            foo = to_grid(P, grid)
            @test locations(foo) == [0.1, 0.3]
            @test weights(foo) == [25.0, 100.0]
            # poles very close to grid
            P = PolesSum([4.0e-16, 0.9999999999999998], [16.0, 25.0])
            grid = [0.0, 1.0]
            foo = to_grid(P, grid)
            @test locations(foo) == [0.0, 1.0]
            @test weights(foo) == [16.0, 25.0]
        end  # to_grid

        @testset "tol_weight_default" begin
            @test tol_weight_default(PolesSum(1:2, [0.25, 0.75])) === 1000 * eps()
            @test tol_weight_default(PolesSum(1:2, [1.0, 3.0])) === 4000 * eps()
            @test tol_weight_default(PolesSum(0:5, 5:10)) === 45_000 * eps()
            @test tol_weight_default(PolesSum(1:2, Float32[0.25, 0.75])) ===
                1000 * eps(Float32)
            @test tol_weight_default(PolesSum(Float64[], Float64[])) === 0.0
        end # tol_weight_default

        @testset "weight" begin
            P = PolesSum([-1.0, 0.0, 0.5], [0.25, 1.5, 2.5])
            @test_throws BoundsError weight(P, 0)
            @test weight(P, 1) == 0.25
            @test weight(P, 2) == 1.5
            @test weight(P, 3) == 2.5
            @test_throws BoundsError weight(P, 4)
        end # weight

        @testset "weights" begin
            P = PolesSum(0:5, 5:10)
            @test weights(P) === P.weights
        end # weights
    end # custom functions

    @testset "Base" begin
        @testset "+" begin
            # addition must merge degenerate poles
            A = PolesSum([1, 3], [4, 5])
            B = PolesSum([2, 3], [6, 7])
            P = A + B
            @test locations(P) == [1, 2, 3]
            @test weights(P) == [4, 6, 12]
        end

        @testset "-" begin
            # unary
            P = PolesSum([1, 2], [3, 4])
            P_new = -P
            @test locations(P_new) == [1, 2]
            @test locations(P_new) !== locations(P)
            @test weights(P_new) == -[3, 4]

            # subtraction must merge degenerate poles
            A = PolesSum([0.1, 0.2], [0.1, 0.25])
            B = PolesSum([0.2], [1])
            P = A - B
            @test locations(P) == [0.1, 0.2]
            @test weights(P) == [0.1, -0.75]
        end # -

        @testset "convert" begin
            P = PolesSum([1, 3], [0, 2])
            P_new = convert(PolesSum{Float64, ComplexF64}, P)
            @test P_new isa PolesSum{Float64, ComplexF64}
            @test locations(P_new) == locations(P)
            @test weights(P_new) == weights(P)
            P_new = convert(PolesSum{Int, Int}, P)
            @test P_new === P
        end # convert

        @testset "copy" begin
            locs = 1:5
            wgts = 6:10
            P = PolesSum(locs, wgts)
            foo = copy(P)
            @test typeof(foo) === typeof(P)
            @test locations(foo) !== locations(P)
            @test locations(foo) == locations(P)
            @test weights(foo) !== weights(P)
            @test weights(foo) == weights(P)
        end # copy

        @testset "eltype" begin
            @test eltype(PolesSum([0, 1], [0, 1])) === Int64
            @test eltype(PolesSum([0, 1], [0.0, 1.0])) === Float64
            @test eltype(PolesSum([0.0, 1.0], [0.0im, 1.0])) === ComplexF64
        end # eltype

        @testset "isempty" begin
            @test isempty(PolesSum(Int[], Float64[]))
            @test !isempty(PolesSum(rand(2), rand(2)))
        end # isempty

        @testset "iterate" begin
            P = PolesSum([0.1, 0.2], [0.3, 0.4])
            @test iterate(P) == ((0.1, 0.3), 1)
            @test iterate(P, 1) == ((0.2, 0.4), 2)
            @test iterate(P, 2) === nothing
        end # iterate

        @testset "length" begin
            @test length(PolesSum(rand(10), rand(10))) === 10
        end # length

        @testset "reverse!" begin
            P = PolesSum([0.1, 0.2], [0.3, 0.4])
            @test reverse!(P) === P
            @test locations(P) == [0.2, 0.1]
            @test weights(P) == [0.4, 0.3]
        end # reverse!

        @testset "reverse" begin
            P = PolesSum([0.1, 0.2], [0.3, 0.4])
            foo = reverse(P)
            @test foo !== P
            @test locations(P) == [0.1, 0.2]
            @test locations(foo) == [0.2, 0.1]
            @test weights(P) == [0.3, 0.4]
            @test weights(foo) == [0.4, 0.3]
        end # reverse

        @testset "show" begin
            P = PolesSum(Int[], Float64[])
            @test sprint(show, P) == "PolesSum{Int64, Float64} with 0 poles"
            P = PolesSum(rand(Int, 1), rand(Float64, 1))
            @test sprint(show, P) == "PolesSum{Int64, Float64} with 1 poles"
            P = PolesSum(rand(Int, 2), rand(Float64, 2))
            @test sprint(show, P) == "PolesSum{Int64, Float64} with 2 poles"
        end # show

        @testset "sort!" begin
            locs = [2, 1]
            wgts = [9, 16]
            P = PolesSum(locs, wgts)
            @test locations(P) == [1, 2]
            @test weights(P) == [16, 9]
        end # sort!
    end # Base

    @testset "LinearAlgebra" begin
        @testset "axpby!" begin
            x = PolesSum([-1.0, 0.0, 1.0], [0.5, 0.75, 2.0])
            y = PolesSum([-2.0, 0.0, 2.0], [0.75, 2.0, 1.0])
            @test axpby!(0.5, x, 2.0, y) === y
            @test locations(y) == [-2.0, -1.0, 0.0, 1.0, 2.0]
            @test weights(y) == [1.5, 0.25, 4.375, 1.0, 2.0]
            # x must be unchanged
            @test locations(x) == [-1.0, 0.0, 1.0]
            @test weights(x) == [0.5, 0.75, 2.0]
        end # axpby!

        @testset "rmul!" begin
            P = PolesSum([-1.0, 0.0, 2.0], [0.5, 1.2, 2.0])
            @test rmul!(P, 2) === P
            @test locations(P) == [-1.0, 0.0, 2.0] # unchanged
            @test weights(P) == [1.0, 2.4, 4.0]
        end # rmul!
    end # LinearAlgebra
end # PolesSum
