# various methods of calculating self-energy from Poles representation

"""
    self_energy_dyson(
        ϵ_imp::Real,
        Δ0::PolesSum,
        G_imp::PolesSum,
        grid::AbstractVector{<:Real}=locations(Δ0),
    )

Calculate the self-energy purely in [`PolesSum`](@ref) representation
using the Dyson equation.

```math
Σ(ω)
= G_{\\mathrm{imp},0}(ω)^{-1} - G_\\mathrm{imp}(ω)^{-1}
= ω - ϵ_\\mathrm{imp} - Δ_0(ω) - G_\\mathrm{imp}(ω)^{-1}
```

Poles with negative weight are moved into neighbors such that the zeroth and first moment
is conserved locally.
"""
function self_energy_dyson(
        ϵ_imp::Real,
        Δ0::PolesSum,
        G_imp::PolesSum,
        grid::AbstractVector{<:Real} = locations(Δ0),
    )

    # invert impurity Green's function
    a0, G_imp_inv = inverse(G_imp)

    # Hartree term
    Σ_H = a0 - ϵ_imp

    # sum of poles
    Σ = G_imp_inv - Δ0
    Σ = to_grid(Σ, grid)
    merge_negative_weight!(Σ)
    remove_zero_weight!(Σ)
    return Σ_H, Σ
end

"""
    self_energy_IFG(C::PolesSumBlock, block::Int = 1)

Given a block sum of poles ``C``, calculate the dynamic part of the self-energy
using the Schur complement
``Σ(z) = I(z) - F^\\mathrm{L}(z) (G(z))^{-1} F^\\mathrm{R}(z)``.

The `block` argument chooses either the top left component (default `1`)
or bottom right (`2`) component.

Returns a `PolesSumBlock` object.

Reference: https://doi.org/10.1103/PhysRevB.105.245132
"""
function self_energy_IFG(C::PolesSumBlock, block::Int = 1)
    T = eltype(C) <: Real ? Float64 : ComplexF64
    N = length(C)
    n = size(C, 1) # block size
    iseven(n) || throw(DomainError(n, "block size of C must be even"))
    block in (1, 2) || throw(DomainError(block, "block must be 1 or 2"))

    B0, HA = anderson_matrix(C)

    # decompose scaling matrix
    F = eigen(Hermitian(B0))
    tol = maximum(F.values) * sqrt(eps(real(T)))
    D = map(λ -> λ >= tol ? 1 / λ : zero(λ), F.values)
    B0_inv = Hermitian(F.vectors * Diagonal(D) * F.vectors') # B0^{-1}
    map!(λ -> λ >= tol ? 1 / λ^2 : zero(λ), D, F.values)
    B0_inv_sqr = Hermitian(F.vectors * Diagonal(D) * F.vectors') # B0^{-2}

    # extract blocks
    A1 = Hermitian(HA[1:n, 1:n])
    A = diag(HA)[(n + 1):end]
    B = view(HA, 1:n, (n + 1):(n * N)) # column vectors b_i

    # take inverse
    A1 = B0_inv * A1 * B0_inv
    amp = B0_inv * B
    P = PolesSumBlock(A, amp)

    # take block
    idx = isone(block) ? (1:(n ÷ 2)) : ((n ÷ 2 + 1):n)
    B0_inv_sqr = Hermitian(B0_inv_sqr[idx, idx])
    A1 = Hermitian(A1[idx, idx])
    for i in eachindex(P)
        weights(P)[i] = weight(P, i)[idx, idx] # potential 1×1 matrix
    end

    # new scaling matrix
    F = eigen(B0_inv_sqr)
    D = map(λ -> λ >= tol ? 1 / sqrt(λ) : zero(λ), F.values)
    B0 = Hermitian(F.vectors * Diagonal(D) * F.vectors') # B0
    A1 = B0 * A1 * B0
    for i in eachindex(P)
        weights(P)[i] = B0 * Hermitian(weight(P, i)) * B0
    end

    # diagonalize
    H = arrowhead_matrix(P)
    H[1:(n ÷ 2), 1:(n ÷ 2)] = A1
    F = eigen(Hermitian(H))
    locs = F.values
    B = view(F.vectors, 1:(n ÷ 2), 1:size(H, 2)) # column vectors b_i
    amp = B0 * B
    P = PolesSumBlock(locs, amp)
    merge_degenerate_poles!(P, zero(real(T)))

    return P
end
